buildscript {
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
    }
}
plugins {
    id 'application'
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.google.protobuf'

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'io.netty:netty-bom:4.1.43.Final'
        mavenBom 'com.linecorp.armeria:armeria-bom:0.97.0'
    }
}

protobuf {
    // Configure the protoc executable.
    protoc {
        // Download from the repository.
        artifact = 'com.google.protobuf:protoc:3.10.1'
    }

    // Locate the codegen plugins.
    plugins {
        // Locate a plugin with name 'grpc'.
        grpc {
            // Download from the repository.
            artifact = 'io.grpc:protoc-gen-grpc-java:1.25.0'
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            grpc {}
        }
    }
}

dependencies {
    compile 'com.linecorp.armeria:armeria'
    compile 'com.linecorp.armeria:armeria-grpc'
    compile 'io.projectreactor:reactor-core:3.3.1.RELEASE'
    compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.5'
    runtime 'org.slf4j:slf4j-simple:1.7.29'

    testCompile 'jakarta.annotation:jakarta.annotation-api:1.3.5'
    testCompile 'junit:junit:4.12'
    testCompile 'net.javacrumbs.json-unit:json-unit-fluent:2.11.1'
    testCompile 'org.assertj:assertj-core:3.14.0'
    testCompile 'org.awaitility:awaitility:4.0.1'
}

application {
    mainClassName = 'example.armeria.grpc.Main'
}

tasks.withType(JavaCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
    options.debug = true
    options.compilerArgs += '-parameters'
}

