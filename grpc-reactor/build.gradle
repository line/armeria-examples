buildscript {
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.3'
    }
}
plugins {
    id 'application'
    id "io.spring.dependency-management" version "1.1.0"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.google.protobuf'

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'io.micrometer:micrometer-bom:1.12.2'
        mavenBom 'io.netty:netty-bom:4.1.106.Final'
        mavenBom 'com.linecorp.armeria:armeria-bom:1.27.0'
        mavenBom 'org.junit:junit-bom:5.10.1'
        mavenBom 'io.github.resilience4j:resilience4j-bom:2.2.0'
    }
}

protobuf {
    // Configure the protoc executable.
    protoc {
        // Download from the repository.
        artifact = 'com.google.protobuf:protoc:3.25.1'
    }

    // Locate the codegen plugins.
    plugins {
        // Locate a plugin with name 'grpc'.
        grpc {
            // Download from the repository.
            artifact = 'io.grpc:protoc-gen-grpc-java:1.61.0'
        }
        // Locate a plugin with name 'reactorGrpc'.
        reactorGrpc {
            // Download from the repository.
            artifact = 'com.salesforce.servicelibs:reactor-grpc:1.2.4'
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            grpc {}
            reactorGrpc {}
        }
    }
}

dependencies {
  implementation 'com.google.code.findbugs:jsr305:3.0.2'
  testImplementation 'junit:junit:4.13.2'
  testImplementation 'org.assertj:assertj-core:3.25.2'
  testImplementation 'org.junit.jupiter:junit-jupiter-api'
  testImplementation 'org.junit.jupiter:junit-jupiter-params'
  testRuntimeOnly 'org.junit.platform:junit-platform-commons'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'
}

dependencies {
    implementation 'com.linecorp.armeria:armeria'
    implementation 'com.linecorp.armeria:armeria-grpc'
    implementation 'com.salesforce.servicelibs:reactor-grpc-stub:1.2.4'
    implementation 'io.projectreactor:reactor-core:3.6.2'
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
    runtimeOnly 'org.slf4j:slf4j-simple:1.7.36'

    testImplementation 'com.linecorp.armeria:armeria-junit5'
    testImplementation 'javax.annotation:javax.annotation-api:1.3.2'
    testImplementation 'net.javacrumbs.json-unit:json-unit-fluent:2.38.0'
    testImplementation 'org.assertj:assertj-core:3.25.2'
    testImplementation 'org.awaitility:awaitility:4.2.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'com.google.protobuf:protobuf-java-util:3.25.1'
}

application {
    mainClass.set('example.armeria.grpc.reactor.Main')
}

tasks.withType(JavaCompile) {
    sourceCompatibility = '17'
    targetCompatibility = '17'
    options.encoding = 'UTF-8'
    options.debug = true
    options.compilerArgs += '-parameters'
}

tasks.withType(Test) {
    useJUnitPlatform()
}

